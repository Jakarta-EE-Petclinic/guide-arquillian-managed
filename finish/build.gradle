buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    // tag::AddLibertyAndDependencyManagementPlugins[]
    dependencies {
        classpath group: 'net.wasdev.wlp.gradle.plugins', name: 'liberty-gradle-plugin', version: '2.4.1'
        classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
    }
    // end::AddLibertyAndDependencyManagementPlugins[]
}

apply plugin: 'java'
// tag::ApplyLibertyAndDependencyManagementPlugins[]
apply plugin: 'liberty'
apply plugin: "io.spring.dependency-management"
// end::ApplyLibertyAndDependencyManagementPlugins[]

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

// tag::AddLibertyServerConfiguration[]
liberty {
    server {
        configFile = file("${rootProject.projectDir}/src/test/resources/server.xml")
        name = 'ArquillianManagedServer'
     }
}
// end::AddLibertyServerConfiguration[]

// tag::AddArquillianBOMDependency[]
dependencyManagement {
    imports {
        mavenBom "org.jboss.arquillian:arquillian-bom:1.4.0.Final"
    }
}
// end::AddArquillianBOMDependency[]

dependencies {
    compile ('javax.enterprise:cdi-api:1.2')
    
    // tag::AddTestDependencyAndOpenLibertyRuntime[]
    testCompile group: "io.openliberty.arquillian", name: "arquillian-liberty-managed-junit", version: "1.1.1"
    testCompile ('org.jboss.shrinkwrap:shrinkwrap-api')
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
    
    libertyRuntime "io.openliberty:openliberty-runtime:[18.0.0.3,)"
    // end::AddTestDependencyAndOpenLibertyRuntime[]
}

// tag::AddTestConfiguration[]
tasks.withType(Test) {
    group 'Verification'
    testLogging.showStandardStreams = true
    reports.html.destination = file("$buildDir/reports/")
    reports.junitXml.destination = file("$buildDir/test-results/")
    include '**/*'
    dependsOn 'installApps', 'testClasses', 'configureArquillian'
} 
// end::AddTestConfiguration[]
